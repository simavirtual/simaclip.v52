/* SIMA - MATRICULA ACADEMICA

MODULO      : PAGOS
SUBMODULO...: PAGOS POR ALUMNO

**************************************************************************
* TITULO..: PAGOS POR ALUMNOS POR NIVELES                                *
**************************************************************************

AUTOR: Nelson Fern ndez G¢mez       FECHA DE CREACION: DIC 03/96 MIE A
       Colombia, Bucaramanga        INICIO: 04:15 PM   DIC 03/96 MIE


REQUERIMIENTOS:

1- Para ejecutar la funci¢n debe tener presente la sintaxis

2- Debe estar en uso el maestro de Estudiantes indexado por grupo y apellido

3- Debe estar en uso el archivo de pagos filtrado por pension o matr¡cula

OBJETIVOS:

1- Permite imprimir los pagos efectuados por matr¡culas o pensi¢n.

2- Retorna Nil

SINTAXIS:


*------------------------------------------------------------------------*
*                              PROGRAMA                                  *
*------------------------------------------------------------------------*/

FUNCTION Matr_5532(aParam1,aParam2,aParam3,;
		   cMatric)

*>>>>DESCRIPCION DE PARAMETROS
/*     aParam1                              // Parametros Generales
       aParam2                              // Parametros Generales
       aParam3                              // Parametros Generales
       cMatric                              // '1'->Matr¡cula '0'->Pensi¢n */
*>>>>FIN DESCRIPCION DE PARAMETROS

*>>>>DECLARACION DE VARIABLES
       #INCLUDE "ARC-MATR.PRG"      // Archivos del Sistema

       LOCAL cSavPan := ''                  // Salvar Pantalla
       LOCAL lHayErr := .F.                 // .T. Hay Error
       LOCAL cIntUno := ''                  // Path de integraci¢n uno
       LOCAL cIntDos := ''                  // Path de integraci¢n dos
       LOCAL cIntTre := ''                  // Path de integraci¢n tres
       LOCAL cIntCua := ''                  // Path de integraci¢n cuatro
     *ÀVariables generales

       LOCAL nCopias := 1                   // N£meros de copias a imprimir
       LOCAL nNroCop := 1                   // N£mero de copia
       LOCAL nTotReg := 0                   // Total registros a imprimir
       LOCAL nRegIni := 0                   // Registro inicial
       LOCAL nRegFin := 0                   // Registro Final
       LOCAL nGruIni := 0                   // Grupo Inicial
       LOCAL cCodGru := ''                  // C¢dgio del grupo
       LOCAL dFecIni := CTOD('00/00/00')    // Fecha Inicial
       LOCAL dFecFin := CTOD('00/00/00')    // Fecha Final
       LOCAL GetList := {}                  // Variable del Sistema
     *ÀVariables espec¡ficas

       FIELD cCodigoGru                     // C¢digo del Grupo
     *ÀVariables de Campo
*>>>>FIN DECLARACION DE VARIABLES

*>>>>DECLARACION PARAMETROS GENERALES
       LOCAL lShared := .T.                 // .T. Sistema Compartido
       LOCAL nModCry := 0                   // Modo de Protecci¢n
       LOCAL cCodSui := ''                  // C¢digo del Sistema
       LOCAL cNomSis := ''                  // Nombre del Sistema
     *ÀDetalles del Sistema

       LOCAL cEmpPal := ''                  // Nombre de la Empresa principal
       LOCAL cNitEmp := ''                  // Nit de la Empresa
       LOCAL cNomEmp := ''                  // Nombre de la Empresa
       LOCAL cNomSec := ''                  // Nombre de la Empresa Secundario
       LOCAL cCodEmp := ''                  // C¢digo de la Empresa
     *ÀDetalles de la Empresa

       LOCAL cNomUsr := ''                  // Nombre del Usuario
       LOCAL cAnoUsr := ''                  // A¤o del usuario
       LOCAL cAnoSis := ''                  // A¤o del sistema
       LOCAL cPatSis := ''                  // Path del sistema
     *ÀDetalles del Usuario

       LOCAL nFilPal := 0                   // Fila Inferior Men£ principal
       LOCAL nFilInf := 0                   // Fila Inferior del SubMen£
       LOCAL nColInf := 0                   // Columna Inferior del SubMen£
     *ÀDetalles Tecnicos

       LOCAL PathW01 := ''                  // Sitio del Sistema No.01
       LOCAL PathW02 := ''                  // Sitio del Sistema No.02
       LOCAL PathW03 := ''                  // Sitio del Sistema No.03
       LOCAL PathW04 := ''                  // Sitio del Sistema No.04
       LOCAL PathW05 := ''                  // Sitio del Sistema No.05
       LOCAL PathW06 := ''                  // Sitio del Sistema No.06
       LOCAL PathW07 := ''                  // Sitio del Sistema No.07
       LOCAL PathW08 := ''                  // Sitio del Sistema No.08
       LOCAL PathW09 := ''                  // Sitio del Sistema No.09
       LOCAL PathW10 := ''                  // Sitio del Sistema No.10
     *ÀSitios del Sistema

       LOCAL PathUno := ''                  // Path de Integraci¢n Uno
       LOCAL PathDos := ''                  // Path de Integraci¢n Dos
       LOCAL PathTre := ''                  // Path de Integraci¢n Tres
       LOCAL PathCua := ''                  // Path de Integraci¢n Cuatro
     *ÀPath de Integraci¢n

       LOCAL cMaeAlu := ''                  // Maestros habilitados
       LOCAL cMaeAct := ''                  // Maestro Activo
       LOCAL cJorTxt := ''                  // Jornada escogida
     *ÀDetalles Acad‚micos

       LOCAL aParams := {}                  // Parametros Generales
*>>>>FIN DECLARACION PARAMETROS GENERALES

*>>>>LECTURA PARAMETROS GENERALES
       aParams := aParams(aParam1,aParam2,aParam3)
       IF  !lParam0101(aParams,;
		       @lShared,@nModCry,@cCodSui,@cNomSis,;
		       @cEmpPal,@cNitEmp,@cNomEmp,@cNomSec,@cCodEmp,;
		       @cNomUsr,@cAnoUsr,@cAnoSis,@cPatSis,;
		       @nFilPal,@nFilInf,@nColInf,;
		       @PathW01,@PathW02,@PathW03,@PathW04,@PathW05,;
		       @PathW06,@PathW07,@PathW08,@PathW09,@PathW10,;
		       @PathUno,@PathDos,@PathTre,@PathCua,;
		       @cMaeAlu,@cMaeAct,@cJorTxt)
	  RETURN NIL
       ENDIF
*>>>>FIN LECTURA PARAMETROS GENERALES

*>>>>SELECION DE LAS AREAS DE TRABAJO
       IF !lUseDbf(.T.,PathUno+'\'+cPatSis+'\'+cMaeAct+'\'+;
		       FileNiv+cAnoSis+ExtFile,'NIV',NIL,lShared)
	  cError('ABRIENDO EL ARCHIVOS')
	  CloseDbf('NIV')
	  RETURN NIL
       ENDIF
*>>>>FIN SELECION DE LAS AREAS DE TRABAJO

*>>>>VALIDACION DE CONTENIDOS DE ARCHIVOS
       lHayErr := .T.
       DO CASE
       CASE NIV->(RECCOUNT()) == 0
	    cError('NO ESTAN GRABADOS LOS NIVELES')

       OTHERWISE
	    lHayErr :=.F.
       ENDCASE

       IF lHayErr
	  CLOSE NIV
	  RETURN NIL
       ENDIF
*>>>>FIN VALIDACION DE CONTENIDOS DE ARCHIVOS

*>>>>LECTURA DEL INTERVALO DE FECHAS
       dFecIni := CTOD('00/00/00')
       dFecFin := CTOD('00/00/00')
       IF ANO->lLeeFecAno
	  IF lPregunta('DESEA EL INFORME POR INTERVALO DE FECHAS? No Si')

	     cSavPan := SAVESCREEN(0,0,24,79)
	     @ 21,20 SAY 'INGRESE LA FECHA EN EL SGTE ORDEN: (MM/DD/AA)'
	     @ nFilInf+1,nColInf SAY 'FECHA INICIAL:' GET dFecIni PICT '@D'
	     @ nFilInf+2,nColInf SAY 'FECHA FINAL  :' GET dFecFin PICT '@D'
	     READ
	     RESTSCREEN(0,0,24,79,cSavPan)

	     IF EMPTY(dFecIni) .OR. EMPTY(dFecFin)
		CloseDbf('NIV')
		RETURN NIL
	     ENDIF

	  ENDIF
       ENDIF
*>>>>FIN LECTURA DEL INTERVALO DE FECHAS

*>>>>CAPTURA DE LOS NIVELES POR INTERVALO
       IF !lInterNiv(nFilInf+1,nColInf,@nRegIni,@nRegFin)
	  CloseDbf('NIV')
	  RETURN NIL
       ENDIF
*>>>>FIN CAPTURA DE LOS NIVELES POR INTERVALO

*>>>>LECTURA DEL NUMERO DE COPIAS
       @ nFilInf+3,nColInf SAY 'NRO COPIAS   :' GET nCopias PICT "99"
       READ
       IF nCopias == 0
	  CloseDbf('NIV')
	  RETURN NIL
       ENDIF
*>>>>FIN LECTURA DEL NUMERO DE COPIAS

*>>>>IMPRESION POR NIVELES
       SELECT NIV
       GO nRegIni
       DO WHILE RECNO() <= nRegFin

**********IMPRESION DE LOS GRUPOS
	    nGruIni := 0
	    DO WHILE nGruIni <= NIV->nNroGruNiv

*--------------FILTRACION DEL MAESTRO
		 cCodGru := NIV->cCodigoNiv+STR(nGruIni,2)
		 lCorrecion(@cCodGru)
		 SELECT &cMaeAct
		 SET FILTER TO cCodigoGru == cCodGru
		 GO TOP
		 IF EOF()
		    nGruIni++
		    SET FILTER TO
		    LOOP
		 ENDIF
		 COUNT TO nTotReg FOR cCodigoGru == cCodGru
		 GO TOP
*--------------FIN FILTRACION DEL MAESTRO

*--------------IMPRESION DE N COPIAS
		 nNroCop := 1
		 DO WHILE nNroCop <= nCopias

*                   IMPRESION DE LA LINEA DE ESTADO
		      LineaEstado('IMPRIMIENDO EL NIVEL: '+NIV->cCodigoNiv+;
			     'ºGRUPO: '+cCodGRu+;
			     'ºNRO COPIAS:'+STR(nNroCop,2)+' DE:'+;
			     STR(nCopias,2)+'ºPOR FAVOR ESPERE ...',cNomSis)
*                   FIN IMPRESION DE LA LINEA DE ESTADO

		    PagosAlu(cNomEmp,cNomSis,cAnoUsr,cMatric,;
			     cJorTxt,cCodGru,cMaeAct,nTotReg,;
			     dFecIni,dFecFin)
		    nNroCop++

		 ENDDO
		 SELECT &cMaeAct
		 SET FILTER TO
*--------------FIN IMPRESION DE N COPIAS

	       nGruIni++

	    ENDDO
**********FIN IMPRESION DE LOS GRUPOS

	  SELECT NIV
	  SKIP

       ENDDO
       CloseDbf('NIV')
       RETURN NIL
*>>>>FIN IMPRESION POR NIVELES